#!/bin/awk -f

# 変数の処理
BEGIN{

	# 区切り文字の処理
	if(fs == ""){

		FS ="," 

	}else{

		FS = fs

	}

	# 文字列の寄せ方の処理
	if(p == "center"){

		padding = "|:----:"

	}else if(p == "right"){
	
		padding = "|----:"

	# "center"でも"right"でもない場合はデフォルトの左寄せ
	}else{

		padding = "|----"

	}

	# 区切り文字を"|"に置換
	OFS = "|"

}

# 1行目のみに処理
NR == 1{

	# OFSを$0に反映させるためのフィールド操作
	$1 = $1

	# 行頭及び行末に"|"を付与
	print "|"$0"|"

	# ヘッダ行以下に挿入
	# iがフィールド数になるまで1づつ増加
	for(i = 1; i <= NF; i++)

		printf padding

	printf "|\n"

}

# 2行目以降の処理
NR != 1{

	$1 = $1

	print "|"$0"|"

}

